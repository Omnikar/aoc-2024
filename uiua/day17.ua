# Experimental!
~ "git: github.com/amatgil/steal-gift" ~ AoCGet
AoCGet 2024 17

°$"_\n\n_"
⊂0∩(⊜⋕∊⇡10⊸-@0)

~[P A B C]
Lit   ← ⊣⊡P
Combo ← ⨬(∘|⋅A|⋅B|⋅C)↥0⊸-3⤚(⊣⊡P)

Adv ← °⊸A⌊÷ⁿ◡⊃⊓Combo2A
Bxl ← °⊸B⍜⋯/≠⊟◡⊃Lit B
Bst ← °⊸B◿8◡Combo
Jnz ← ⨬⊙∘(°⊸P-2◡Lit)≠0⊸A
Bxc ← °⊸B⍜⋯/≠⊟⊸⊃B C
Out ← ⊃⊙∘(⊂◿8Combo)
Bdv ← °⊸B⌊÷ⁿ◡⊃⊓Combo2A
Cdv ← °⊸C⌊÷ⁿ◡⊃⊓Combo2A

Step ← (
  ◡(⊢⊡P)
  ⨬(Adv|Bxl|Bst|Jnz|Bxc|Out|Bdv|Cdv)
  ⍜P+₂
  >◡⊓P⧻)

Part₁ ← &p/$"_,_"⇌⋅◌⍢∘Step ⊙⊓⧈₂∘[]
Part₂ ← ⍤"Sorry, I did part 2 by hand… no code here"0

⨬Part₁ Part₂ -₁⋕⊡₁&args
